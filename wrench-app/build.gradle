apply plugin: 'com.android.application'

android {

    dataBinding {
        enabled = true
    }

    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.izettle.wrench"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 7
        versionName "1.0.6"
        testInstrumentationRunner "com.izettle.wrench.WrenchTestRunner"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        def configurationAuthorityValue = applicationId + ".configprovider"

        manifestPlaceholders = [configurationAuthority: configurationAuthorityValue]

        buildConfigField "String", "CONFIG_AUTHORITY", "\"${configurationAuthorityValue}\""

    }
    testOptions {
        execution 'ANDROID_TEST_ORCHESTRATOR'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-stetho.pro'
        }
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-stetho.pro'

            versionNameSuffix " debug"
            applicationIdSuffix = ".debug"
        }
    }
    lintOptions {
        baseline file("lint-baseline.xml")
        checkReleaseBuilds true
        abortOnError true
        warningsAsErrors true
        lintConfig file("../lint.xml")
    }

}

dependencies {
    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestUtil 'com.android.support.test:orchestrator:1.0.1'

    annotationProcessor "android.arch.lifecycle:compiler:1.0.0-alpha9-1"
    annotationProcessor "android.arch.persistence.room:compiler:1.0.0-alpha9-1"
    annotationProcessor "com.google.dagger:dagger-android-processor:2.11"
    annotationProcessor "com.google.dagger:dagger-compiler:2.11"

    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'android.arch.lifecycle:runtime:1.0.0'
    implementation 'android.arch.lifecycle:extensions:1.0.0-alpha9-1'
    implementation 'android.arch.persistence.room:runtime:1.0.0-alpha9-1'
    implementation "android.arch.lifecycle:extensions:1.0.0-alpha9-1"

    implementation "com.google.dagger:dagger:2.11"
    implementation "com.google.dagger:dagger-android:2.11"
    implementation "com.google.dagger:dagger-android-support:2.11"

    implementation project(':wrench-core')
    implementation project(':wrench-prefs')

}
